import sun.tools.jar.resources.jar

apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'scala'

repositories {
    mavenCentral()

    ivy {
        ivyPattern "${projectDir}/play-2.1.1/repository/local/[organisation]/[module]/[revision]/ivys/ivy.xml"
        artifactPattern "${projectDir}/play-2.1.1/repository/local/[organisation]/[module]/[revision]/jars/[module].[ext]"
    }
}

dependencies {
    compile 'org.hamcrest:hamcrest-core:1.1'
    compile 'org.scalatest:scalatest_2.10:1.9.1'

    compile 'com.typesafe.akka:akka-actor_2.10:2.1.4'
    compile group: 'play', name: 'play_2.10', version: '2.1.1', configuration: 'compile'
}

sourceSets {
    main {
        scala {
            srcDirs = ['src/main']
        }
    }

    test {
        scala {
            srcDirs = ['src/test']
        }
    }
}

class CalculateValidBoardProbabilityRule {
    static final prefix = 'calculateValid'
    static final suffix = 'BoardProbability'
}

tasks.addRule("Pattern: ${CalculateValidBoardProbabilityRule.prefix}<Type>${CalculateValidBoardProbabilityRule.suffix}") { String taskName ->
    if (taskName.startsWith(CalculateValidBoardProbabilityRule.prefix) && taskName.endsWith(CalculateValidBoardProbabilityRule.suffix)) {
        task(taskName, type: sfc.CalculateValidBoardProbabilityTask) {
            // FIXME: this shouldn't be needed if proper inputs are set
            dependsOn = [build]

            boardConfigurationType = taskName - CalculateValidBoardProbabilityRule.prefix - CalculateValidBoardProbabilityRule.suffix
        }
    }
}

class GenerateValidBoardConfigurationRule {
    static final prefix = 'generateValid'
    static final suffix = 'BoardConfiguration'
}

tasks.addRule("Pattern: ${GenerateValidBoardConfigurationRule.prefix}<Type>${GenerateValidBoardConfigurationRule.suffix}") { String taskName ->
    if (taskName.startsWith(GenerateValidBoardConfigurationRule.prefix) && taskName.endsWith(GenerateValidBoardConfigurationRule.suffix)) {
        task(taskName, type: sfc.GenerateValidBoardConfigurationTask) {
            // FIXME: this shouldn't be needed if proper inputs are set
            dependsOn = [build]

            boardConfigurationType = taskName - GenerateValidBoardConfigurationRule.prefix - GenerateValidBoardConfigurationRule.suffix
        }
    }
}
